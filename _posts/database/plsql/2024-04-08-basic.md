---
title: PL/SQL - 기본 문법
date: 2024-04-08 09:10:00 +09:00
categories: [DB, pl-sql]
tags:
  [
    Data,
    DB,
    Datebase,
    oracle,
    sql,
    pl-sql
  ]
---

## Ⅰ. PL-SQL이란?

### 1. PL-SQL이란

> Oracle's Procedural Language extension to SQL의 약자  
> 오라클 자체에 내장되어 있는 Procedure Language

- SQL문장에서 변수정의, 조건처리(IF), 반복처리(LOOP, WHILE, FOR)등을 지원
- DECLARE문을 이용하여 정의되며, 선언문의 사용은 선택 사항
- PL/SQL 문은 블록 구조로 되어 있고, PL/SQL 자신이 컴파일 엔진을 보유

<hr>

### 2. DBMS 출력창 사용

- Tool > 보기 > DBMS 출력창 > + 버튼 클릭 > 사용자 접속(개발자)
- DBMS 출력창에서 실행문 결과 확인

<hr>

### 3. 기본 구조

- `DECLARE` : 변수 선언
- `BEGIN` : 실행문의 시작을 알림
- `END` : 실행문의 끝
- `DBMS_OUTPUT.PUT_LINE` : 화면 출력

```sql
DECLARE --선언
  vno number(4);
  vname varchar2(20);
BEGIN  -- 실행문 시작
  vno := 100; -- 변수 할당
  vname := 'kglim';
  DBMS_OUTPUT.PUT_LINE(vno); --화면 출력
  DBMS_OUTPUT.PUT_LINE(vname || '입니다');
END;  -- 실행문 끝
```

<hr><br>

## Ⅱ. 변수

### 1. 변수 선언

`DECLARE` 선언부에 `변수명 타입`으로 변수 선언 실행
- `:=` 연산자를 통해 초기값 할당 가능

```sql
DECLARE
v_job varchar2(10); -- 변수 선언
v_date date := sysdate + 7; -- 초기값 설정 가능
v_valid boolean not null := true -- not null 설정 가능
```

<hr>

## 2. 변수에 값 할당

기본적으로 `:=` 연산자를 통해 선언한 변수에 값 할당

```sql
DECLARE --선언
  vno number(4);
  vname varchar2(20);
BEGIN
  vno := 100; -- 할당 >  String s; s = "홍길동"
  vname := 'kglim';
  DBMS_OUTPUT.PUT_LINE(vno); --화면 출력
  DBMS_OUTPUT.PUT_LINE(vname || '입니다');
END;
```

`SELECT`문을 활용하여 변수에 테이블의 특정 데이터 값을 담을 수도 있다.

```sql
DECLARE -- 변수 선언
  vno number(4);
  vname varchar2(20);
BEGIN
   select empno ,ename 
      into vno , vname --into : 실행결과 변수에 담기
   from emp
   where empno=&empno; --& : 입력값 받기
   
   DBMS_OUTPUT.PUT_LINE('변수값 : ' || vno || '/' || vname);
END;
```

<hr>

### 3. 변수 타입 제어

기본적으로 선언문에서 타입을 직접 지정
- `v_empno number(10)`

`%TYPE` : 테이블에 있는 컬럼의 타입 사용 
- `v_empno emp.empno%TYPE`  

`%ROWTYPE` : 테이블 모든 컬럼 타입 정보를 사용(한 행을 모두 담을 변수)
- `v_row emp%ROWTYPE`

```sql
DECLARE
  v_emprow emp%ROWTYPE; 
BEGIN
  select *
    into v_emprow -- empno , ename , ,..... deptno
  from emp
  where empno=7788;
  
  DBMS_OUTPUT.PUT_LINE(v_emprow.empno || '-' || v_emprow.ename || '-' || v_emprow.sal);
END;
```

<hr><br>

## Ⅱ. 제어문

### 1. IF문

기본 구조는 다음과 같다.

```sql
IF(condition2) THEN statement1  
ELSIF(condition2) THEN statement2  
...
ELSE statementN  -- option
ENDIF;
```

이때 `ELSEIF`가 아닌 `ELSIF`라는 점과 `ENDIF`를 꼭 넣어야한다는 점을 기억하자

```sql
DECLARE
  vempno emp.empno%TYPE;
  vename emp.ename%TYPE;
  vdeptno emp.deptno%TYPE;
  vname varchar2(20) := null;
BEGIN
    select empno , ename , deptno
        into vempno , vename , vdeptno --변수
    from emp
    where empno=7499;

    IF(vdeptno = 10) THEN vname := 'ACC';
        ELSIF(vdeptno=20) THEN vname := 'IT';
        ELSIF(vdeptno=30) THEN vname := 'SALES';
    END IF;
    DBMS_OUTPUT.PUT_LINE('당신의 직종은 : ' || vname);
END;
```

<hr>

### 2. CASE문

기본 구조는 다음과 같다.

```sql
CASE
    WHEN condition1 THEN
        statement1;
    WHEN condition2 THEN
        statement2;
    ...
    WHEN conditionN THEN
        statementN;
    ELSE
        default_statement;
END;
```

CASE문 또한 `END`로 그 끝을 선언해줘야한다.

```sql
DECLARE
  vempno emp.empno%TYPE;
  vename emp.ename%TYPE;
  vdeptno emp.deptno%TYPE;
  v_name varchar2(20);
BEGIN
     select empno, ename , deptno
        into vempno, vename , vdeptno
     from emp
     where empno=7499;

    v_name := CASE 
                WHEN vdeptno=10  THEN 'AA'
                WHEN vdeptno in(20,30)  THEN 'BB'
                WHEN vdeptno=40  THEN 'CC'
                ELSE 'NOT'
              END;
    DBMS_OUTPUT.PUT_LINE('당신의 부서명:' || v_name);            
END;
```

<hr><br>

## Ⅳ. 반복문

### 1. LOOP

기본 형식은 다음과 같다

```sql
LOOP
  statement
  EXIT WHEN (condition)
END LOOP
```

`EXIT WHEN`을 통해 특정 조건에서 종료시켜줘야한다.

```sql
DECLARE
  n number :=0;
BEGIN
  LOOP
    DBMS_OUTPUT.PUT_LINE('n value : ' || n);
    n := n + 1;
    EXIT WHEN n > 5;
  END LOOP;
END;
```

<hr>

### 2. WHILE

`WHILE`을 통해 탈출 조건을 구문의 최상단에 위치 시킬 수 있다.

```sql
DECLARE
  num number := 0;
BEGIN
  WHILE(num < 6)
    LOOP
      DBMS_OUTPUT.PUT_LINE('num 값 : ' || num);
      num := num +1;
    END LOOP;
END;
```

<hr>

### 3. FOR

`FOR`문 또한 사용 가능하다.
- `n..m` : `n`부터 `m`까지 반복

```sql
BEGIN
  FOR i IN 0..10 LOOP
    DBMS_OUTPUT.PUT_LINE(i);
  END LOOP;
END;
```

<hr>

### 4. CONTINUE

11g버전 이후 `CONTINUE`를 통한 반복문 스킵이 가능해졌다.

```sql
DECLARE
  total number := 0;
BEGIN
  FOR i IN 1..100 LOOP
    DBMS_OUTPUT.PUT_LINE('변수 : ' || i);
    CONTINUE WHEN i > 5; --skip
    total := total + i;
  END LOOP;
    DBMS_OUTPUT.PUT_LINE('합계 : ' || total);
END;
```

<hr>

### ※ RETURN하는 행의 개수

질의는 하나의 행만 RETURN 해야 한다.

- PL/SQL는 블록 내의 SELECT 문장은 다음 규칙을 적용하는 Embedded SQL 의 ANSI 범주에 속한다.
- 질의의 결과는 하나의 행만을 RETURN 해야 하고 하나의 행 이상 또는 행이 없는 것은 에러를 생성
- `NO_DATA_FOUND` 와 `TOO_MANY_ROWS`를 예외로 블록의 예외 섹션에서 추적할 수 있는 표준 예외를 조성하여 처리

```sql
DECLARE
    v_empno emp.empno%TYPE;
    v_name  emp.ename%TYPE := UPPER('&name');
    v_sal   emp.sal%TYPE;
    v_job   emp.job%TYPE;
    v_deptno emp.deptno%TYPE;
BEGIN
  select empno , job ,sal , deptno
    into v_empno, v_job , v_sal , v_deptno
  from emp
  where ename = v_name;
  
  IF v_job IN('MANAGER','ANALYST') THEN
     v_sal := v_sal * 1.5;
  ELSE
     v_sal := v_sal * 1.2;
  END IF;
  
  update emp
  set sal = v_sal
  where deptno=v_deptno;
  
  DBMS_OUTPUT.PUT_LINE(SQL%ROWCOUNT || '개의 행이 갱신 되었습니다');
  
  --예외처리
  EXCEPTION
    WHEN NO_DATA_FOUND THEN
       DBMS_OUTPUT.PUT_LINE(v_name || '는 자료가 없습니다');
    WHEN TOO_MANY_ROWS THEN
       DBMS_OUTPUT.PUT_LINE(v_name || '는 동명 이인입니다');
    WHEN OTHERS THEN
       DBMS_OUTPUT.PUT_LINE('기타 에러가 발생했습니다');
END;

SELECT * FROM EMP;
```

<hr><br>

## Ⅴ. 예외 처리

### 1. 기본 예외 처리

기본 문법은 다음과 같다

```sql
EXCEPTION
  WHEN exceptiontype1 THEN
      statement1;
  WHEN exceptiontype2 THEN
      statement2;
  WHEN exceptiontype3 THEN
      statement3;
```

PL/SQL 내부에서 정의한 여러가지 예외가 존재한다.

```sql
DECLARE
  ...
BEGIN
  ...
  --예외처리
  EXCEPTION
    WHEN NO_DATA_FOUND THEN
       DBMS_OUTPUT.PUT_LINE(v_name || '는 자료가 없습니다');
    WHEN TOO_MANY_ROWS THEN
       DBMS_OUTPUT.PUT_LINE(v_name || '는 동명 이인입니다');
    WHEN OTHERS THEN
       DBMS_OUTPUT.PUT_LINE('기타 에러가 발생했습니다');
END;
```

<hr>

### 2. 사용자 정의 예외

사용자가 원하는 상황에 강제로 예외를 던질 수 있다.
- `RAISE_APPLICATION_ERROR(에러코드, 에러메시지)`

```sql
DECLARE
v_ename emp.ename%TYPE := '&p_ename';
v_err_code NUMBER;
v_err_msg VARCHAR2(255);
BEGIN
  DELETE emp WHERE ename = v_ename;
      
  IF SQL%NOTFOUND THEN
  RAISE_APPLICATION_ERROR(-20002,'my no data found'); --사용자가 예외 만들기 (강제 예외 던지기)
  END IF;

  EXCEPTION 
  WHEN OTHERS THEN
      ROLLBACK;
        v_err_code := SQLCODE;
        v_err_msg := SQLERRM;
        DBMS_OUTPUT.PUT_LINE('에러 번호 : ' || TO_CHAR(v_err_code));
        DBMS_OUTPUT.PUT_LINE('에러 내용 : ' || v_err_msg);
END;
```